plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.epages.restdocs-api-spec' version '0.18.4'
}

group = 'com.project'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    /* =========================
       Spring Boot
    ========================= */
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'

    /* =========================
        Spring Security
    ============================ */
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    /* =========================
        Lombok
    ========================= */
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    /* =========================
       QueryDSL (Boot 3 / Jakarta)
    ========================= */
    implementation "com.querydsl:querydsl-jpa:5.0.0:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"

    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    /* =========================
       Swagger
   ========================= */
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.3'

    /* =========================
       AWS S3
   ========================= */
    implementation 'software.amazon.awssdk:s3:2.41.23'

    /* ========================
       JSOUP
    =========================*/
    implementation 'org.jsoup:jsoup:1.22.1'

    /* ========================
       IP 검증
    =========================*/
    implementation 'commons-validator:commons-validator:1.7'

    /* =========================
       Test
    ========================= */
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.18.4'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation('it.ozimov:embedded-redis:0.7.3') {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }

}

tasks.named('test') {
    useJUnitPlatform()
}

/* =========================
   QueryDSL Generated Source
   ========================= */

def querydslDir = layout.buildDirectory.dir("generated/querydsl").get().asFile

tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(querydslDir)
}

sourceSets {
    main {
        java {
            srcDir querydslDir
        }
    }
}

tasks.named("clean") {
    delete querydslDir
}

/* =========================
   REST Docs → OpenAPI
   ========================= */
openapi3 {
    server = 'http://localhost'
    title = 'Admin System API'
    description = 'Rest Docs를 이용해 생성된 Swagger 문서입니다.'
    version = '0.0.1'
    format = 'yaml'
}

tasks.register('copySwaggerToStatic', Copy) {
    dependsOn 'openapi3'
    from("build/api-spec/openapi3.yaml")
    into("src/main/resources/static/docs")
}

tasks.named('build') {
    dependsOn 'copySwaggerToStatic'
}